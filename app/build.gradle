apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'https://github.com/uPhyca/stetho-realm/raw/master/maven-repo' }
}

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'com.github.triplet.play'
apply from: '../config/quality/quality.gradle'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "chat.rocket.android"
        minSdkVersion 16
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 54
        versionName "1.0.31"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }

    signingConfigs {
        release {
            storeFile project.rootProject.file('Rocket.jks').getCanonicalFile()
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }

    buildTypes {
        debug {
            debuggable true
            versionNameSuffix '-DEBUG'
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }

        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/rxjava.properties'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        //avoiding okio error: https://github.com/square/okhttp/issues/896
        lintConfig file("lint.xml")
    }

    sourceSets {
        debug {
            manifest.srcFile 'src/debug/AndroidManifest.xml'
        }

        release {
            manifest.srcFile 'src/release/AndroidManifest.xml'
        }

        test {
            test.java.srcDirs += 'src/test/kotlin'
            androidTest.java.srcDirs += 'src/androidTest/kotlin'
        }
    }

    dexOptions {
        if(System.getenv()["CIRCLECI"] as boolean) {
            javaMaxHeapSize "1536M"
            preDexLibraries false
        }
    }
}

play {
    jsonFile = file('rocket-chat.json')
    track = "${track}"
}
ext {
    playLibVersion = '11.6.0'
    stethoVersion = '1.5.0'
    stethoOkhttp3Version = '1.5.0'
    stethoRealmVersion = '2.1.0'
    rxbindingVersion = '2.0.0'
    rxlifecycleVersion = '2.1.0'
    icepickVersion = '3.2.0'
    permissionsdispatcherVersion = '2.4.0'
}

dependencies {
    compile project(':android-ddp')
    compile project(':rocket-chat-android-widgets')
    compile project(':persistence-realm')
    compile extraDependencies.okHTTP
    compile extraDependencies.rxJava
    compile extraDependencies.boltTask
    compile supportDependencies.multidex
    compile supportDependencies.designSupportLibrary
    compile supportDependencies.annotation
    compile rxbindingDependencies.rxBinding
    compile rxbindingDependencies.rxBindingSupport
    compile rxbindingDependencies.rxBindingAppcompact
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$rootProject.ext.kotlinVersion"
    compile "com.google.firebase:firebase-core:$playLibVersion"
    compile "com.google.firebase:firebase-crash:$playLibVersion"
    compile "com.google.android.gms:play-services-gcm:$playLibVersion"
    compile "com.trello.rxlifecycle2:rxlifecycle:$rxlifecycleVersion"
    compile "com.trello.rxlifecycle2:rxlifecycle-android:$rxlifecycleVersion"
    compile "com.trello.rxlifecycle2:rxlifecycle-components:$rxlifecycleVersion"
    compile 'nl.littlerobots.rxlint:rxlint:1.2'
    compile "frankiesardo:icepick:$icepickVersion"
    annotationProcessor "frankiesardo:icepick-processor:$icepickVersion"
    compile "com.github.hotchemi:permissionsdispatcher:$permissionsdispatcherVersion"
    annotationProcessor "com.github.hotchemi:permissionsdispatcher-processor:$permissionsdispatcherVersion"
    compile('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true;
    }
    debugCompile "com.facebook.stetho:stetho:$stethoVersion"
    debugCompile "com.facebook.stetho:stetho-okhttp3:$stethoOkhttp3Version"
    debugCompile "com.uphyca:stetho_realm:$stethoRealmVersion"
    debugCompile "com.tspoon.traceur:traceur:1.0.1"
    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.3'
    testCompile "org.jetbrains.kotlin:kotlin-test:$rootProject.ext.kotlinVersion"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$rootProject.ext.kotlinVersion"
    testCompile "org.jetbrains.kotlin:kotlin-reflect:$rootProject.ext.kotlinVersion"
    testCompile "com.nhaarman:mockito-kotlin:1.5.0"
    testCompile 'org.amshove.kluent:kluent:1.14'
}
apply plugin: 'com.google.gms.google-services'
